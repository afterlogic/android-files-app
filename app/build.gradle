apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'org.greenrobot.greendao'

project.ext.set("autoIncrementTasks", "assembleReleaseBeta")
apply from: '../artifacts.gradle'

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {

        applicationId "com.afterlogic.aurora.files"
        minSdkVersion 15
        targetSdkVersion 25

        prepareVersions()
        versionCode getIncrementalVersionCode()
        versionName getIncrementalVersionName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    dataBinding{
        enabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    signingConfigs {

        Properties signingProperties = new Properties()
        File propsFile = new File("signing/signing.properties")
        if(propsFile.exists()) {
            signingProperties.load(new FileInputStream(propsFile))
        } else {
            throw new IllegalStateException("Signing properties file not exist! File: " + propsFile.absolutePath)
        }

        sign {
            storeFile file(signingProperties['keystore'])
            storePassword signingProperties['keystore.password']
            keyAlias signingProperties['key.alias']
            keyPassword signingProperties['key.password']
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.sign
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.sign
            minifyEnabled false
            ext.enableCrashlytics = false
        }

        releaseBeta {
            signingConfig signingConfigs.sign
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ext.betaDistributionGroupAliases = "aurora-android"
            ext.betaDistributionReleaseNotesFilePath = projectDir.absolutePath + "/lastBetaBuildNotes.txt"
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

greendao {
    schemaVersion 1
}

ext{
    greenDaoSchemeVersion = 1

    //Dependencies
    compatVersion = '25.4.0'
    daggerVersion = '2.11'
    rxJavaVersion = '2.1.1'
    rxJavaAndroidVersion = '2.0.1'
    retrofitVersion = '2.3.0'
    okHttpVersion = '3.8.1'
    glideVersion = '3.8.0'
    photoViewVersion = '1.3.1'
    fabVersion = '1.10.1'
    gsonVersion = '2.8.1'
    streamVersion = '1.1.8'
    constraintLayoutVersion = '1.1.0-beta1'
    eventBusVersion = '3.0.0'
    greenDaoVersion = '3.2.2'
    trayPrefsVersion = '0.12.0'
    crashlyticsVersion = '2.6.8@aar'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //For test
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:1.10.19'

    //Dagger - Dependency Injection
    implementation "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    //Compat
    implementation "com.android.support:appcompat-v7:${compatVersion}"
    implementation "com.android.support:design:${compatVersion}"
    implementation "com.android.support:recyclerview-v7:${compatVersion}"
    implementation "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"

    //Observable and Stream
    implementation "com.annimon:stream:${streamVersion}"
    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxJavaAndroidVersion}"

    //[START Api helpers]
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    //[END Api helpers]

    //Floating action button
    implementation "com.getbase:floatingactionbutton:${fabVersion}"
    //Material progress bar
    implementation 'me.zhanghai.android.materialprogressbar:library:1.1.5'

    //DB
    implementation "org.greenrobot:greendao:${greenDaoVersion}"

    //EventBus
    implementation "org.greenrobot:eventbus:${eventBusVersion}"

    //Pictures load
    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    implementation "com.github.chrisbanes:PhotoView:${photoViewVersion}"

    //Preferences
    implementation "net.grandcentrix.tray:tray:${trayPrefsVersion}"

    //Crash and use analytics
    implementation("com.crashlytics.sdk.android:crashlytics:${crashlyticsVersion}") {
        transitive = true;
    }

    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
}