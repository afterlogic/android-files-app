apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply plugin: 'org.greenrobot.greendao'

project.ext.set("autoIncrementTasks", "assembleReleaseBeta")
apply from: '../artifacts.gradle'

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {

        applicationId "com.afterlogic.aurora.files"
        minSdkVersion 15
        targetSdkVersion 25

        prepareVersions()
        versionCode getIncrementalVersionCode()
        versionName getIncrementalVersionName()

        //Tray preferences
        resValue "string", "tray__authority", "${applicationId}.tray"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    dataBinding{
        enabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    signingConfigs {

        Properties signingProperties = new Properties()
        File propsFile = new File("signing/signing.properties");
        if(propsFile.exists()) {
            signingProperties.load(new FileInputStream(propsFile))
        } else {
            throw new IllegalStateException("Signing properties file not exist! File: " + propsFile.absolutePath)
        }

        sign {
            storeFile file(signingProperties['keystore'])
            storePassword signingProperties['keystore.password']
            keyAlias signingProperties['key.alias']
            keyPassword signingProperties['key.password']
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.sign
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.sign
            minifyEnabled false
            ext.enableCrashlytics = false
        }

        releaseBeta {
            signingConfig signingConfigs.sign
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ext.betaDistributionGroupAliases = "aurora-android"
            ext.betaDistributionReleaseNotesFilePath = projectDir.absolutePath + "/lastBetaBuildNotes.txt"
        }
    }
}

greendao {
    schemaVersion 1
}

ext{
    greenDaoSchemeVersion = 1

    //Dependencies
    compatVersion = '25.3.1'
    daggerVersion = '2.9'
    rxJavaVersion = '2.0.8'
    rxJavaAndroidVersion = '2.0.1'
    retrofitVersion = '2.1.0'
    retrofitRxJava2AdapterVersion = '1.0.0'
    okHttpVersion = '3.7.0'
    glideVersion = '3.7.0'
    photoViewVersion = '1.3.1'
    fabVersion = '1.10.1'
    gsonVersion = '2.8.0'
    streamVersion = '1.1.2'
    constraintLayoutVersion = '1.0.2'
    eventBusVersion = '3.0.0'
    greenDaoVersion = '3.2.0'
    trayPrefsVersion = '0.11.0'
    crashlyticsVersion = '2.6.3@aar'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    //For test
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'

    //Dagger - Dependency Injection
    compile "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    //Compat
    compile "com.android.support:appcompat-v7:${compatVersion}"
    compile "com.android.support:design:${compatVersion}"
    compile "com.android.support:recyclerview-v7:${compatVersion}"
    compile "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"

    //Observable and Stream
    compile "com.annimon:stream:${streamVersion}"
    compile "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    compile "io.reactivex.rxjava2:rxandroid:${rxJavaAndroidVersion}"

    //[START Api helpers]
    compile ("com.squareup.retrofit2:retrofit:${retrofitVersion}")
    compile ("com.squareup.retrofit2:converter-gson:${retrofitVersion}")
    compile ("com.jakewharton.retrofit:retrofit2-rxjava2-adapter:${retrofitRxJava2AdapterVersion}")
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    compile "com.google.code.gson:gson:${gsonVersion}"
    //[END Api helpers]

    //Floating action button
    compile "com.getbase:floatingactionbutton:${fabVersion}"
    //Material progress bar
    compile 'me.zhanghai.android.materialprogressbar:library:1.1.5'

    //DB
    compile "org.greenrobot:greendao:${greenDaoVersion}"

    //EventBus
    compile "org.greenrobot:eventbus:${eventBusVersion}"

    //Pictures load
    compile "com.github.bumptech.glide:glide:${glideVersion}"
    compile "com.github.chrisbanes:PhotoView:${photoViewVersion}"

    //Preferences
    compile "net.grandcentrix.tray:tray:${trayPrefsVersion}"

    //Crash and use analytics
    compile("com.crashlytics.sdk.android:crashlytics:${crashlyticsVersion}") {
        transitive = true;
    }
}