// Core versions
ext.versions = [
    compileSdk: 28,
    targetSdk: 28,
    minSdk: 17,
    buildTools: '28.0.2',
    core: [
        androidPlugin: '3.3.0-alpha11'
    ]
]

// Additional versions which may reference to core versions
ext.versions += [
    core: [
            androidPlugin: versions.core.androidPlugin,
            dagger: '2.16'
    ],
    kotlin: [
        kotlin: '1.3.0',
        reflectLite: '1.0.0',
        ktx: [
            core: '1.0.0'
        ],
        serialization: [
            runtime: '0.8.0-rc13'
        ]
    ],
    androidx: [
        compat: '1.0.0',
        material: '1.0.0',
        constraint: '1.1.3',
        flex: '1.1.0',
        viewPager: '1.0.0',
        multidex: '2.0.0',
        lifeCycle: '2.0.0',
        room: '2.0.0',
        workmanager: '1.0.0-alpha09',
        navigation: '1.0.0-alpha05'
    ],
    googleServices: [
        plugin: '3.0.0',//'4.1.0',
        general: '15.0.1',
        location: '15.0.1',
        gcm: '15.0.1'
    ],
    firebase: [
        core: '16.0.3',
        messaging: '17.3.2',
        analytics: '16.0.3',
        jobDispatcher: '0.8.5'
    ],
    dataBinding: [
        lastAdapter: '2.3.0.2',
        compiler: versions.core.androidPlugin
    ],
    network: [
        gson: '2.8.5',
        retrofit: '2.4.0',
        okhttp: '3.11.0',
        glide: '4.8.0',
        kotlinxConverter: '0.0.1'
    ],
    rx: [
        java: '2.2.2',
        android: '2.1.0',
        kotlin: '2.3.0',
        modern: '1.2.2'
    ],
    greendao: [
        dao: '3.2.2',
        eventbus: '3.1.1'
    ],
    utils: [
        inputMask: '3.4.4',
        trayPrefs: '0.12.0',
        cicerone: '3.0.0.2',
        mvvmkit: '0.0.11',
        streams: '1.2.1',
        parceler: '1.1.11',
        mapstruct: '1.3.0.Beta1',
        guava: '26.0-android',
        typetools: '0.5.0',
        calligraphy: '2.3.0',
        annimonStreams: '1.2.1'
    ],
    view: [
        circularImageView: '3.2.0',
        fab: '1.10.1',
        materialProgressBar: '1.4.2',
        calendar: [
            applandeo: '1.4.0',
            prolific: '2.0.0'
        ],
        photoView: '2.1.4',
        spanny: '1.0.4',
        mapbox: '6.5.0'
    ],
    debug: [
        fabricPlugin: '1.25.4',
        crashlytics: '2.9.5',
        timber: '4.7.1',
        stetho: '1.5.0',
        leakCanary: '1.6.1',
        sqlScout: '4.1'
    ],
    plugins: [
        versions: '0.20.0',
        dexcount: '0.8.3'
    ],
    tests: [
        junit: '4.12',
        mockito: '2.22.0',
        espresso: '3.1.0-alpha4',
        runner: '1.1.0-alpha4'
    ]
]

ext.deps = [
    core: [
        // Android
        androidPlugin: "com.android.tools.build:gradle:$versions.core.androidPlugin",
        googleServicesPlugin: "com.google.gms:google-services:$versions.googleServices.plugin",

        // Dagger
        dagger: "com.google.dagger:dagger:$versions.core.dagger",
        daggerAndroid: "com.google.dagger:dagger-android:$versions.core.dagger",
        daggerAndroidSupport: "com.google.dagger:dagger-android-support:$versions.core.dagger",
        daggerAndroidProcessor: "com.google.dagger:dagger-android-processor:$versions.core.dagger",
        daggerCompiler: "com.google.dagger:dagger-compiler:$versions.core.dagger"
    ],
    googleServices: [
        location: "com.google.android.gms:play-services-location:$versions.googleServices.location",
        gcm: "com.google.android.gms:play-services-gcm:$versions.googleServices.gcm"
    ],
    kotlin: [
        stdlib: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin.kotlin",
        reflect: "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin.kotlin",
        reflectLite: "org.jetbrains.kotlinx:kotlinx.reflect.lite:$versions.kotlin.reflectLite",
        plugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin.kotlin",
        extensionsPlugin: "org.jetbrains.kotlin:kotlin-android-extensions:$versions.kotlin.kotlin",
        ktx: [
            core: "androidx.core:core-ktx:$versions.kotlin.ktx.core"
        ],
        serialization: [
            plugin: "org.jetbrains.kotlin:kotlin-serialization:$versions.kotlin.kotlin",//"org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$versions.kotlin.serialization.plugin",
            runtime: "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$versions.kotlin.serialization.runtime"
        ],
    ],
    firebase: [
        core: "com.google.firebase:firebase-core:$versions.firebase.core",
        analytics: "com.google.firebase:firebase-analytics:$versions.firebase.analytics",
        messaging: "com.google.firebase:firebase-messaging:$versions.firebase.messaging",
        jobDispatcher: "com.firebase:firebase-jobdispatcher:$versions.firebase.jobDispatcher"
    ],
    androidx: [
        appcompat: "androidx.appcompat:appcompat:$versions.androidx.compat",
        material: "com.google.android.material:material:$versions.androidx.material",
        recyclerView: "androidx.recyclerview:recyclerview:$versions.androidx.compat",
        card: "androidx.cardview:cardview:$versions.androidx.compat",
        constraintLayout: "androidx.constraintlayout:constraintlayout:$versions.androidx.constraint",
        flexLayout: "com.google.android:flexbox:$versions.androidx.flex",
        viewPager: "androidx.viewpager:viewpager:$versions.androidx.viewPager",
        multidex: [
            api: "androidx.multidex:multidex:$versions.androidx.multidex",
            instrumentation: "androidx.multidex:multidex-instrumentation:$versions.androidx.multidex"
        ],
        arch: [
            coreTesting: "androidx.arch.core:core-testing:$versions.androidx.lifeCycle"
        ],
        lifecycle: [
            runtime: "androidx.lifecycle:lifecycle-runtime:$versions.androidx.lifeCycle",
            viewModel: "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.androidx.lifeCycle",
            compiler: "androidx.lifecycle:lifecycle-compiler:$versions.androidx.lifeCycle",
            extensions: "androidx.lifecycle:lifecycle-extensions:$versions.androidx.lifeCycle"
        ],
        navigation: [
            fragment: "android.arch.navigation:navigation-fragment-ktx:$versions.androidx.navigation",
            ui: "android.arch.navigation:navigation-ui-ktx:$versions.androidx.navigation",
            testing: "android.arch.navigation:navigation-testing-ktx:$versions.androidx.navigation",
            safeArgsPlugin: "android.arch.navigation:navigation-safe-args-gradle-plugin:$versions.androidx.navigation"
        ],
        room: [
            runtime: "androidx.room:room-runtime:$versions.androidx.room",
            rx: "androidx.room:room-rxjava2:$versions.androidx.room",
            compiler: "androidx.room:room-compiler:$versions.androidx.room",
            testing: "androidx.room:room-testing:$versions.androidx.room"
        ],
        workmanager: [
            runtime: "android.arch.work:work-runtime:$versions.androidx.workmanager",
            ktx: "android.arch.work:work-runtime-ktx:$versions.androidx.workmanager",
            testing: "android.arch.work:work-testing:$versions.androidx.workmanager"
        ]
    ],
    dataBinding: [
        compiler: "androidx.databinding:databinding-compiler:$versions.dataBinding.compiler",
        lastAdapter: "me.sunnydaydev:lastadapter:$versions.dataBinding.lastAdapter"
    ],
    rx: [
        java: "io.reactivex.rxjava2:rxjava:$versions.rx.java",
        android: "io.reactivex.rxjava2:rxandroid:$versions.rx.android",
        kotlin: "io.reactivex.rxjava2:rxkotlin:$versions.rx.kotlin",
        modern: "me.sunnydaydev:modern-rx:$versions.rx.modern@aar"
    ],
    network: [
        gson: "com.google.code.gson:gson:$versions.network.gson",
        retrofit: "com.squareup.retrofit2:retrofit:$versions.network.retrofit",
        converterGson: "com.squareup.retrofit2:converter-gson:$versions.network.retrofit",
        converterScalars: "com.squareup.retrofit2:converter-scalars:$versions.network.retrofit",
        rxAdapter: "com.squareup.retrofit2:adapter-rxjava2:$versions.network.retrofit",
        okhttp: "com.squareup.okhttp3:okhttp:$versions.network.okhttp",
        logging: "com.squareup.okhttp3:logging-interceptor:$versions.network.okhttp",
        glide: "com.github.bumptech.glide:glide:$versions.network.glide",
        glideCompiler: "com.github.bumptech.glide:compiler:$versions.network.glide",
        kotlinxConverter: "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:$versions.network.kotlinxConverter"
    ],
    debug: [
        fabricPlugin: "io.fabric.tools:gradle:$versions.debug.fabricPlugin",
        crashlytics: "com.crashlytics.sdk.android:crashlytics:$versions.debug.crashlytics@aar",
        timber: "com.jakewharton.timber:timber:$versions.debug.timber",
        stetho: [
            api: "com.facebook.stetho:stetho:$versions.debug.stetho",
            okhttp: "com.facebook.stetho:stetho-okhttp3:$versions.debug.stetho"
        ],
        leakCanary: [
            api: "com.squareup.leakcanary:leakcanary-android:$versions.debug.leakCanary",
            noop: "com.squareup.leakcanary:leakcanary-android-no-op:$versions.debug.leakCanary"
        ],
        sqlScout: "com.idescout.sql:sqlscout-server:$versions.debug.sqlScout"
    ],
    utils: [
        inputMask: "com.redmadrobot:inputmask:$versions.utils.inputMask",
        prefs: "net.grandcentrix.tray:tray:$versions.utils.trayPrefs",
        cicerone: "me.sunnydaydev:cicerone:$versions.utils.cicerone",
        mvvmkit: "me.sunnydaydev:mvvmkit:$versions.utils.mvvmkit@aar",
        streams: "com.annimon:stream:$versions.utils.streams",
        parceler: [
            api: "org.parceler:parceler-api:$versions.utils.parceler",
            compiler: "org.parceler:parceler:$versions.utils.parceler"
        ],
        mapstruct: [
            api: "org.mapstruct:mapstruct-jdk8:$versions.utils.mapstruct",
            processor: "org.mapstruct:mapstruct-processor:$versions.utils.mapstruct"
        ],
        guava: "com.google.guava:guava:$versions.utils.guava",
        typetools: "net.jodah:typetools:$versions.utils.typetools",
        calligraphy: "uk.co.chrisjenx:calligraphy:$versions.utils.calligraphy",
        annimonStreams: "com.annimon:stream:$versions.utils.annimonStreams"
    ],
    greendao: [
        plugin: "org.greenrobot:greendao-gradle-plugin:$versions.greendao.dao",
        dao: "org.greenrobot:greendao:$versions.greendao.dao",
        eventbus: "org.greenrobot:eventbus:$versions.greendao.eventbus"
    ],
    view: [
        circularImageView: "com.mikhaellopez:circularimageview:$versions.view.circularImageView",
        fab: "com.getbase:floatingactionbutton:$versions.view.fab",
        materialProgressBar: "me.zhanghai.android.materialprogressbar:library:$versions.view.materialProgressBar",
        calendar: [
            prolific: "com.github.prolificinteractive:material-calendarview:$versions.view.calendar.prolific",
            applandeo: "com.applandeo:material-calendar-view:$versions.view.calendar.applandeo"
        ],
        photoView: "com.github.chrisbanes:PhotoView:$versions.view.photoView",
        spanny: "com.binaryfork:spanny:$versions.view.spanny",
        mapbox: "com.mapbox.mapboxsdk:mapbox-android-sdk:$versions.view.mapbox"
    ],
    plugins: [
        versions: "com.github.ben-manes:gradle-versions-plugin:$versions.plugins.versions",
        dexcount: "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$versions.plugins.dexcount"
    ],
    tests: [
        junit: "junit:junit:$versions.tests.junit",
        mockito: "org.mockito:mockito-core:$versions.tests.mockito",
        espresso: "androidx.test.espresso:espresso-core:$versions.tests.espresso",
        runner: "androidx.test:runner:$versions.tests.runner"
    ]
]

ext.deps += [
    shortcut: [
        rx: { dependenciesRx() },
        kotlin: { dependenciesKotlin() },
        dagger: { kapt -> dependenciesDagger(kapt)}
    ]
]

ext.handleRepositories = { handler, buildScript ->

    handler.maven { url "https://kotlin.bintray.com/kotlinx" }
    handler.google()
    handler.jcenter()
    handler.mavenCentral()
    handler.maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    handler.maven { url 'https://maven.fabric.io/public' }
    handler.maven { url "http://dl.bintray.com/populov/maven" }

    if (!buildScript) {
        handler.maven { url 'https://jitpack.io' }
    }

    handler.maven { url 'http://www.idescout.com/maven/repo/' }

    handler.flatDir { dirs 'libs' }

    // Local project maven
    handler.maven {
        def internalMaven = "$project.rootProject.projectDir/maven/"
        url project.uri(internalMaven)
    }

}

// Shortcut functions

void dependenciesRx() {

    dependencies {
        implementation deps.rx.java
        implementation deps.rx.android
        implementation deps.rx.kotlin
        implementation deps.rx.modern
    }

}

void dependenciesKotlin() {

    dependencies {
        implementation deps.kotlin.stdlib
        implementation deps.kotlin.ktx
    }

}

void dependenciesDagger(boolean useKapt) {

    dependencies {

        implementation deps.core.dagger
        kapt deps.core.daggerCompiler
        if (useKapt) {
            kapt deps.core.daggerAndroidProcessor
        }

    }

}

ext.defaultAndroidModule = {

    android {

        compileSdkVersion versions.compileSdk
        buildToolsVersion versions.buildTools

        defaultConfig {

            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk

            Properties versionsProps = new Properties()
            file("$rootProject.projectDir/meta/version.properties").withInputStream {
                versionsProps.load(it)
            }

            versionCode versionsProps['version.code'].toInteger()
            versionName versionsProps['version.name']

            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        }

        buildTypes {

            release {
                // minifyEnabled false
                // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }

        }

    }

}

ext.defaultAndroidSingnedModule = {

    defaultAndroidModule()

    android {

        if (!privateFile('props.properties').exists()) return

        signingConfigs {

            release {
                storeFile file("$project.rootProject.rootDir/${privateProperty('keystore.file')}")
                storePassword privateProperty('keystore.password')
                keyAlias privateProperty('keystore.release.alias')
                keyPassword privateProperty('keystore.release.password')
            }

        }

        buildTypes {

            release {

                signingConfig signingConfigs.release

            }

        }

    }

}

ext.privateFile = { String path ->

    File file = file("$rootProject.projectDir/private/$path")

    if (file == null) {
        throw new IllegalArgumentException("Private file $path not exists.")
    }

    return file

}

ext.privateProperty = { String name ->

    File file = privateFile("props.properties")

    Properties props = new Properties()
    props.load(new FileInputStream(file))

    String property = props[name]

    if (property == null) {
        throw new IllegalArgumentException("Private property $name not exists.")
    }

    return property

}